plugins {
    id "io.spring.dependency-management" version "1.0.1.RELEASE"
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"


android {

    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version
    defaultConfig {
        applicationId "com.settler"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }

    productFlavors {
    }

}

ext {
    dagger_version = "2.10"
//    rxbinding_version = "2.0.0"
}

kapt {
    generateStubs = true
}

dependencyManagement {
    imports {
        mavenBom "io.projectreactor:reactor-bom:Bismuth-M2"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //Rx
    compile 'io.projectreactor:reactor-core'

    //Dependency Injection
    compile "com.google.dagger:dagger-android:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    provided 'javax.annotation:javax.annotation-api:1.3'

    //Compatibility
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:recyclerview-v7:$android_compat_version"
    compile "com.android.support:appcompat-v7:$android_compat_version"
    compile "com.android.support:design:$android_compat_version"
    compile "com.android.support:palette-v7:$android_compat_version"

    //Tests
    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.4-SNAPSHOT'
    testCompile 'com.nhaarman:mockito-kotlin:1.5.0'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'io.projectreactor:reactor-test'

    implementation project(':reactor')
}

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugUnitTest"
    into "build/intermediates/classes/debug"
}

task copyInstrumentationTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugAndroidTest"
    into "build/intermediates/classes/androidTest"
}

task copySdkClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debug"
    into "build/intermediates/classes/debug"
}

afterEvaluate {
    compileDebugUnitTestSources.dependsOn copyTestClasses
//    compileReleaseUnitTestSources.dependsOn copyTestClasses

    compileDebugAndroidTestSources.dependsOn copySdkClasses
    compileDebugAndroidTestSources.dependsOn copyInstrumentationTestClasses
}